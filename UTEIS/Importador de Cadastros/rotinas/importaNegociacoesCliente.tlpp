/*/{Protheus.doc} User Function importaNegociacoesCliente
  Rotina para importacao de veiculos
  @type  Function
  @author Fabricio Ferreira
  @since 04/09/2023
  @version 1.0
  @param cArquivo, character, Arquivo a ser importado
  /*/
namespace Duofy.importadorCadastros
User Function importaNegociacoesCliente(cArquivo as character)

	Local cHeader           as character
	Local aHeader           as array
	Local cLinha             as character
	Local aLinha             as array
	Local nIndex            as numeric
	Local nLinha            as numeric
	Local aCampos           as array
	Local cCGC              as character
	Local cCodCli           as character
	Local cLoja             as character
	Local cChaveU52         as character
	Local cItem             as character
	Local cErro             as character
	Local cProduto          as character
	Local cGrupoPro         as character
	Local cFormaPG          as character
	Local cCondPG           as character
	Local cGrupoCli       	as character
	Local cDescricaoProduto as character
	Local cDescricaoGrupo 	as character
	Local cTipoRegra		as character
	Local cIntLj			as character
	Local lErroLinha        as logical
	Local oFile    := FWFileReader():New(cArquivo)
	Local lRecLock := .T.
	
	aCampos := {}
	AAdd(aCampos,"A1_CGC")
	AAdd(aCampos,"U53_CODCLI")
	AAdd(aCampos,"U53_LOJA")
	AAdd(aCampos,"U53_GRPVEN")
	AAdd(aCampos,"U53_FORMPG")
	AAdd(aCampos,"U53_CONDPG")
	AAdd(aCampos,"U53_CODPRO")
	AAdd(aCampos,"U53_GRUPO")
	AAdd(aCampos,"U53_TPRGNG")
	if U53->(FieldPos("U53_INTLJ")) > 0
		AAdd(aCampos,"U53_INTLJ")
	endif

	If !oFile:Open()
		MsgStop( "Falha ao abrir o arquivo: " + cArquivo, 'Importador Cadastros' )
		Return
	Endif

	cHeader := oFile:GetLine()
	aHeader := StrToKArr2(cHeader, ";",.T.)
	ProcRegua(0)
	IncProc("Processando Cabeçalho...")

	cErro := ""
	For nIndex := 1 to Len(aHeader)
		If aScan(aCampos, aHeader[nIndex]) <= 0
			cErro += "Linha 1 - Campo invalido: " + aHeader[nIndex] + Chr(13)+Chr(10)
		EndIf
	Next nIndex

	If !Empty(cErro)
		Aviso( "Importador Cadastros", cErro, { "Fechar" }, 3, 'Erros encontrados no arquivo '+cArquivo,,,.T.)
		Return
	EndIf

	nLinha:=0
	cChaveU52 := ""
	cItem := "000"
	While oFile:hasLine()
		lErroLinha := .F.
		nLinha++
		cLinha := oFile:GetLine()
		aLinha := StrToKArr2(cLinha, ";",.T.)
		
		cCGC := Space(TamSX3("A1_CGC")[1])
		cCodCli := Space(TamSX3("U53_CODCLI")[1])
		cLoja   := Space(TamSX3("U53_LOJA")[1])
		cFormaPG := Space(TamSX3("U53_FORMPG")[1])
		cCondPG := Space(TamSX3("U53_CONDPG")[1])
		cProduto := Space(TamSX3("U53_CODPRO")[1])
		cGrupoPro := Space(TamSX3("U53_GRUPO")[1])
		cGrupoCli	:= Space(TamSX3("U53_GRPVEN")[1])
		cTipoRegra := "R" //default é regra
		cIntLj := " "

		if aScan(aHeader,"A1_CGC") > 0
			cCGC := Pad(aLinha[aScan(aHeader,"A1_CGC")], TamSX3("A1_CGC")[1])
		endif
		if aScan(aHeader,"U53_CODCLI") > 0
			cCodCli := Pad(aLinha[aScan(aHeader,"U53_CODCLI")], TamSX3("U53_CODCLI")[1])
		endif
		if aScan(aHeader,"U53_LOJA") > 0
			cLoja := Pad(aLinha[aScan(aHeader,"U53_LOJA")], TamSX3("U53_LOJA")[1])
		endif
		if aScan(aHeader,"U53_GRPVEN") > 0
			cGrupoCli := Pad(aLinha[aScan(aHeader,"U53_GRPVEN")], TamSX3("U53_GRPVEN")[1])
		endif
		if aScan(aHeader,"U53_FORMPG") > 0
			cFormaPG := Pad(aLinha[aScan(aHeader,"U53_FORMPG")], TamSX3("U53_FORMPG")[1])
		endif
		if aScan(aHeader,"U53_CONDPG") > 0
			cCondPG := Pad(aLinha[aScan(aHeader,"U53_CONDPG")], TamSX3("U53_CONDPG")[1])
		endif
		if aScan(aHeader,"U53_CODPRO") > 0
			cProduto := Pad(aLinha[aScan(aHeader,"U53_CODPRO")], TamSX3("U53_CODPRO")[1])
		endif
		if aScan(aHeader,"U53_GRUPO") > 0
			cGrupoPro := Pad(aLinha[aScan(aHeader,"U53_GRUPO")], TamSX3("U53_GRUPO")[1])
		endif
		if aScan(aHeader,"U53_TPRGNG") > 0
			cTipoRegra := Pad(aLinha[aScan(aHeader,"U53_TPRGNG")], TamSX3("U53_TPRGNG")[1])
		endif
		if aScan(aHeader,"U53_INTLJ") > 0
			cIntLj := Pad(aLinha[aScan(aHeader,"U53_INTLJ")], TamSX3("U53_INTLJ")[1])
		endif

		IncProc("Processando Linha "+cValToChar(nLinha)+"...")

		SX5->(DbSetOrder(1))
		If !SX5->(DbSeek(xFilial()+"24"+cFormaPG))
			cErro += "Linha "+cValToChar(nLinha+1)+" - Forma de pagamento não encontrada: " + cFormaPG + Chr(13)+Chr(10)
			lErroLinha := .T.
		EndIf

		SE4->(DbSetOrder(1))
		If !SE4->(DbSeek(xFilial()+cCondPG))
			cErro += "Linha "+cValToChar(nLinha+1)+" - Condição de pagamento não encontrada: " + cCondPG + Chr(13)+Chr(10)
			lErroLinha := .T.
		EndIf

		U44->(DbSetOrder(1))
		If !U44->(DbSeek(xFilial()+cFormaPG+cCondPG))
			cErro += "Linha "+cValToChar(nLinha+1)+" - Negociação de pagamento não encontrada: " + cFormaPG+cCondPG + Chr(13)+Chr(10)
			lErroLinha := .T.
		EndIf

		if !(cTipoRegra $ 'R,E')
			cErro += "Linha "+cValToChar(nLinha+1)+" - Forma de pagamento não encontrada: " + cFormaPG + Chr(13)+Chr(10)
			lErroLinha := .T.
		endif
		
		If cChaveU52 != cCGC+cCodCli+cLoja+cGrupoCli
			SA1->(DbSetOrder(3))
			If !empty(cCGC) .AND. !SA1->(DbSeek(xFilial()+cCGC))
				cErro += "Linha "+cValToChar(nLinha+1)+" - CNPJ/CPF não encontrado: " + cCGC + Chr(13)+Chr(10)
				lErroLinha := .T.
			EndIf
			SA1->(DbSetOrder(1))
			If !empty(cCodCli) .AND. !SA1->(DbSeek(xFilial()+cCodCli+cLoja))
				cErro += "Linha "+cValToChar(nLinha+1)+" - Cliente não encontrado: " + cCodCli+cLoja + Chr(13)+Chr(10)
				lErroLinha := .T.
			EndIf
			ACY->(DbSetOrder(1))
			if !empty(cGrupoCli) .AND. !ACY->(DbSeek(xFilial()+cGrupoCli))
				cErro += "Linha "+cValToChar(nLinha+1)+" - Grupo Cliente não encontrado: " + cGrupoCli + Chr(13)+Chr(10)
				lErroLinha := .T.
			endif
			if !lErroLinha
				cChaveU52 := cCGC+cCodCli+cLoja+cGrupoCli
			
				U52->(DbSetOrder(1))//U52_FILIAL+U52_CODCLI+U52_LOJA+U52_GRPVEN+U52_CLASSE+U52_SATIV1
				if !empty(cCGC) .OR. !empty(cCodCli)
					lRecLock := !U52->(DbSeek(xFilial()+SA1->A1_COD+SA1->A1_LOJA))
				else
					lRecLock := !U52->(DbSeek(xFilial()+Space(TamSX3("A1_COD")[1])+Space(TamSX3("A1_LOJA")[1])+ACY->ACY_GRPVEN))
				endif
				if lRecLock
					cItem := "000"
				else
					cItem := BuscaItem() //"000"
				endif
			endif
		EndIf

		U53->(DbSetOrder(3)) //U53_FILIAL+U53_FORMPG+U53_CONDPG+U53_CODCLI+U53_LOJA
		If (!empty(cCGC) .OR. !empty(cCodCli)) .AND. U53->(DbSeek(xFilial()+cFormaPG+cCondPG+SA1->A1_COD+SA1->A1_LOJA))
			While U53->(!Eof()) .AND. U53->U53_FILIAL+U53->U53_FORMPG+U53->U53_CONDPG+U53->U53_CODCLI+U53->U53_LOJA == xFilial("U53")+cFormaPG+cCondPG+SA1->A1_COD+SA1->A1_LOJA
				if U53->U53_TPRGNG+U53->U53_CODPRO+U53->U53_GRUPO == cTipoRegra+cProduto+cGrupoPro
					cErro += "Linha "+cValToChar(nLinha+1)+" - Negociação "+cFormaPG+cCondPG+" ja cadastrada para o cliente." + Chr(13)+Chr(10)
					lErroLinha := .T.
					exit
				endif
				U53->(DBSkip())
			enddo
		elseif !empty(cGrupoCli)
			U53->(DbSetOrder(2)) //U53_FILIAL+U53_FORMPG+U53_CONDPG
			U53->(DbSeek(xFilial()+cFormaPG+cCondPG))
			While U53->(!Eof()) .AND. U53->U53_FILIAL+U53->U53_FORMPG+U53->U53_CONDPG == xFilial("U53")+cFormaPG+cCondPG
				if U53->U53_GRPVEN == cGrupoCli .AND. U53->U53_TPRGNG+U53->U53_CODPRO+U53->U53_GRUPO == cTipoRegra+cProduto+cGrupoPro
					cErro += "Linha "+cValToChar(nLinha+1)+" - Negociação "+cFormaPG+cCondPG+" ja cadastrada para o grupo." + Chr(13)+Chr(10)
					lErroLinha := .T.
					exit
				endif
				U53->(DBSkip())
			enddo
		endif

		cDescricaoProduto := ""
		cDescricaoGrupo := ""
		If !Empty(AllTrim(cProduto))
			SB1->(DbSetOrder(1))
			If SB1->(DbSeek(xFilial()+cProduto))
				cProduto:= SB1->B1_COD
				cDescricaoProduto:= SB1->B1_DESC
			Else
				cErro += "Linha "+cValToChar(nLinha+1)+" - Produto não encontrado: " + cProduto + Chr(13)+Chr(10)
				lErroLinha := .T.
			EndIf
		EndIf
		If !Empty(AllTrim(cGrupoPro))
			SBM->(DbSetOrder(1))
			If SBM->(DbSeek(xFilial()+cGrupoPro))
				cGrupoPro:= SBM->BM_GRUPO
				cDescricaoGrupo:= SBM->BM_DESC
			Else
				cErro += "Linha "+cValToChar(nLinha+1)+" - Grupo de Produto não encontrado: " + cGrupoPro + Chr(13)+Chr(10)
				lErroLinha := .T.
			EndIf
		EndIf
		If !lErroLinha
			Begin Transaction
				If lRecLock //cItem == "000"
					RecLock("U52",lRecLock)
					U52->U52_FILIAL := xFilial("U52")
					if !empty(cCGC) .OR. !empty(cCodCli)
						U52->U52_CODCLI := SA1->A1_COD
						U52->U52_LOJA := SA1->A1_LOJA
					else
						U52->U52_GRPVEN := cGrupoCli
					endif
					U52->(MsUnLock())
					lRecLock := .F.
				EndIf
				
				cItem := Soma1(cItem)
				RecLock("U53",.T.)
				U53->U53_FILIAL:= xFilial("U53")
				if !empty(cCGC) .OR. !empty(cCodCli)
					U53->U53_CODCLI:= SA1->A1_COD
					U53->U53_LOJA  := SA1->A1_LOJA
				else
					U53->U53_GRPVEN := cGrupoCli
				endif
				U53->U53_ITEM  := cItem
				U53->U53_FORMPG:= cFormaPG
				U53->U53_CONDPG:= cCondPG
				U53->U53_DESCFP:= AllTrim(SX5->X5_DESCRI)
				U53->U53_DESCPG:= AllTrim(SE4->E4_DESCRI)
				U53->U53_DESCRI:= AllTrim(SX5->X5_DESCRI) + " " + AllTrim(SE4->E4_DESCRI)
				U53->U53_TPRGNG:= cTipoRegra
				U53->U53_CODPRO:= cProduto
				U53->U53_DESCPR:= cDescricaoProduto
				U53->U53_GRUPO := cGrupoPro
				U53->U53_DESCGR:= cDescricaoGrupo
				if U53->(FieldPos("U53_INTLJ")) > 0
					U53->U53_INTLJ := cIntLj
				endif
				U53->(MsUnLock())
			End Transaction
		EndIf
	EndDo

	oFile:Close()
	If !Empty(cErro)
		Aviso( "Importador Cadastros", cErro, { "Fechar" }, 3, 'Erros encontrados no arquivo '+cArquivo,,,.T.)
	EndIf

Return

Static Function BuscaItem()
	Local cQry
	Local cRet := ""
	
	cQry := "SELECT MAX(U53_ITEM) PROX "
	cQry += " FROM " + RetSqlName("U53")
	cQry += " WHERE U53_FILIAL = '"+U52->U52_FILIAL+"' "
	cQry += " AND U53_CODCLI = "+ValToSQL(U52->U52_CODCLI)
	cQry += " AND U53_LOJA = "+ValToSQL(U52->U52_LOJA)
	cQry += " AND U53_GRPVEN = "+ValToSQL(U52->U52_GRPVEN)
	cQry += " AND D_E_L_E_T_ = ' ' "
	cQry := ChangeQuery(cQry)

	cRet := MpSysExecScalar(cQry, "PROX")

	If Empty(cRet)
		cRet := "000"
	EndIf
Return cRet
