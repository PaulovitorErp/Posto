
/*/{Protheus.doc} User Function PPILimCred
Carrega dados de limite de credito

Fontes externos que devem ser compilados para correto funcionamento:
TRETE032
TPDVP007

@type  Function
@author thebritto
@since 08/12/2020
@version version
/*/
User Function PPILimCred()

    Local cReturn 
    Local cCgc
    Local cCodCli
    Local cGrupo
    Local cRetVlrLim := "[0,0]"
    Local cRetLimUs := "[0,0]"
    Local cRetTX := "Falha ao buscar limite de credito."
    Local cRetSt := "false"
    Local cGrpCli := ""
    Local aLimRet

    If HTTPPOST->CGC != NIL .and. HTTPPOST->CODCLI != NIL .and. HTTPPOST->GRUPO != NIL

        cCgc := HTTPPOST->CGC
        cCodCli := HTTPPOST->CODCLI
        cGrupo := HTTPPOST->GRUPO

        if empty(cCgc+cCodCli+cGrupo)
            cRetTX := "Preencha um dos campos para buscar os dados de limite de credito"
        else

            if !empty(cCgc+cCodCli)
                if !empty(cCgc)
                    SA1->(DbSetOrder(3))
                    SA1->(DbSeek(xFilial("SA1")+alltrim(cCgc) ))
                else
                    SA1->(DbSetOrder(1))
                    SA1->(DbSeek(xFilial("SA1")+alltrim(cCodCli) ))
                endif
                if SA1->(!Eof())
                    
                    cRetTX := "<h3 class='text-primary'>"+Alltrim(SA1->A1_NOME)+"</h3>"
                    cRetTX += "<p>"+Alltrim(SA1->A1_NREDUZ)+"</p>"
                    cRetTX += "Codigo / Loja: <b>"+SA1->A1_COD+ " / " + SA1->A1_LOJA + "</b><br>"
                    cRetTX += "CPF/CNPJ: <b>"+Alltrim(SA1->A1_CGC)+"</b><br>"
                    cRetTX += "Bloqueado: <b>"+iif(SA1->A1_MSBLQL == "1","SIM","NAO")+"</b><br>"
                    if !empty(SA1->A1_GRPVEN)
                        cRetTX += "<a href='javascript:verGrupo();'>Grupo: <b>"+SA1->A1_GRPVEN + " - " + Alltrim(Posicione("ACY",1,xFilial("ACY")+SA1->A1_GRPVEN,"ACY_DESCRI"))+"</b></a><br>"
                        cGrpCli := SA1->A1_GRPVEN
                    endif
                    cRetTX += "Tipo Nota: <b>"+iif(U_TPDVP007(SA1->A1_COD, SA1->A1_LOJA, .T.)==1,"NFC-e","NF-e")+"</b><br>"
                    
                    cRetTX += "<hr>"

                    aLimRet := U_TRETE032(1,{{SA1->A1_COD,SA1->A1_LOJA,''}})
                    
                    cRetTX += "Blq. Credito: <b>"+iif(aLimRet[1][5] == "1","SIM","NAO")+"</b><br>"
                    cRetTX += "Limite Credito Global: <b>R$ "+Alltrim(Transform(aLimRet[1][3],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "Vlr Utilizado Global: <b>R$ "+Alltrim(Transform(aLimRet[1][1],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "<span class='"+iif(aLimRet[1][3] - aLimRet[1][1] >= 0,"text-success","text-danger")+"'>"
                    cRetTX += "Saldo Global: <b>R$ "+Alltrim(Transform(aLimRet[1][3] - aLimRet[1][1],PesqPict("SA1","A1_XLC")))+"</b></span><br>"

                    cRetVlrLim := "["+cValToChar(aLimRet[1][3])+","
                    cRetLimUs := "["+cValToChar(aLimRet[1][1])+","

                    aLimRet := U_TRETE032(2,{{SA1->A1_COD,SA1->A1_LOJA,''}})

                    cRetTX += "Blq. Saque: <b>"+iif(aLimRet[1][5] == "1","SIM","NAO")+"</b><br>"
                    cRetTX += "Limite Credito Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][3],PesqPict("SA1","A1_XLIMSQ")))+"</b><br>"
                    cRetTX += "Vlr Utilizado Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][1],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "<span class='"+iif(aLimRet[1][3] - aLimRet[1][1] >= 0,"text-success","text-danger")+"'>"
                    cRetTX += "Saldo Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][3] - aLimRet[1][1],PesqPict("SA1","A1_XLC")))+"</b></span><br>"

                    cRetVlrLim += cValToChar(aLimRet[1][3])+"]"
                    cRetLimUs += cValToChar(aLimRet[1][1])+"]"
                    
                    cRetTX += "<hr>"

                    cRetTX += "Emitente Cheque: <b>"+iif(SA1->A1_XEMCHQ == "S","SIM","NAO")+"</b><br>"
                    cRetTX += "Emitente Carta Frete: <b>"+iif(SA1->A1_XEMICF == "S","SIM","NAO")+"</b><br>"
                    cRetTX += "Vale Servi&ccedil;o P&oacute;s: <b>"+iif(SA1->A1_XVLSPOS == "S","SIM","NAO")+"</b><br>"

                    cRetTX += "<br>"
                    cRetSt := "true"
                else
                    cRetTX := "Nenhum cliente encontrado!"
                endif

            elseif !empty(cGrupo)
                ACY->(DbSetOrder(1))
                if ACY->(DbSeek(xFilial("ACY")+alltrim(cGrupo) ))
                    
                    cRetTX := "<h3 class='text-primary'>"+Alltrim(ACY->ACY_DESCRI)+"</h3>"
                    cRetTX += "Codigo Grupo: <b>"+ACY->ACY_GRPVEN + "</b><br>"

                    cRetTX += "<hr>"

                    aLimRet := U_TRETE032(1,{{'','',ACY->ACY_GRPVEN}})
                    
                    cRetTX += "Blq. Credito: <b>"+iif(aLimRet[1][6] == "1","SIM","NAO")+"</b><br>"
                    cRetTX += "Limite Credito Global: <b>R$ "+Alltrim(Transform(aLimRet[1][4],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "Vlr Utilizado Global: <b>R$ "+Alltrim(Transform(aLimRet[1][2],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "<span class='"+iif(aLimRet[1][4] - aLimRet[1][2] >= 0,"text-success","text-danger")+"'>"
                    cRetTX += "Saldo Global: <b>R$ "+Alltrim(Transform(aLimRet[1][4] - aLimRet[1][2],PesqPict("SA1","A1_XLC")))+"</b></span><br>"

                    cRetVlrLim := "["+cValToChar(aLimRet[1][4])+","
                    cRetLimUs := "["+cValToChar(aLimRet[1][2])+","

                    aLimRet := U_TRETE032(2,{{'','',ACY->ACY_GRPVEN}})

                    cRetTX += "Blq. Saque: <b>"+iif(aLimRet[1][6] == "1","SIM","NAO")+"</b><br>"
                    cRetTX += "Limite Credito Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][4],PesqPict("SA1","A1_XLIMSQ")))+"</b><br>"
                    cRetTX += "Vlr Utilizado Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][2],PesqPict("SA1","A1_XLC")))+"</b><br>"
                    cRetTX += "<span class='"+iif(aLimRet[1][4] - aLimRet[1][2] >= 0,"text-success","text-danger")+"'>"
                    cRetTX += "Saldo Saque: <b>R$ "+Alltrim(Transform(aLimRet[1][4] - aLimRet[1][2],PesqPict("SA1","A1_XLC")))+"</b></span><br>"

                    cRetVlrLim += cValToChar(aLimRet[1][4])+"]"
                    cRetLimUs += cValToChar(aLimRet[1][2])+"]"

                    cRetTX += "<br>"
                    cRetSt := "true"

                else
                    cRetTX := "Nenhum grupo de cliente encontrado!"
                endif
            endif

        endif

    endif

    cReturn := '{"vlrLimites":'+cRetVlrLim+', "limUsado":'+cRetLimUs+', "textos":"'+cRetTX+'","success":'+cRetSt+',"vergrupo":"'+cGrpCli+'"}'

Return cReturn
